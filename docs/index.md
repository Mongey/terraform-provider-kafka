---
page_title: "kafka Provider"
subcategory: ""
description: |-
  A Terraform provider for managing Apache Kafka clusters and resources.
---

# kafka Provider

The Kafka provider is used to interact with [Apache Kafka](https://kafka.apache.org/) clusters. The provider allows you to manage Kafka topics, ACLs, quotas, and SCRAM credentials. It supports various authentication methods including TLS, SASL/PLAIN, SASL/SCRAM, AWS IAM, and OAuth.

## Documentation

- [Quick Start Guide](guides/quick-start) - Get started quickly with common scenarios
- [Authentication Guide](guides/authentication) - Detailed authentication configuration  
- [AWS MSK Integration](guides/aws-msk-integration) - Complete MSK setup guide
- [Migration Guide](guides/migration) - Migrate between setups and versions
- [Troubleshooting Guide](guides/troubleshooting) - Common issues and solutions

## Example Usage

### Basic Configuration with TLS

```terraform
terraform {
  required_providers {
    kafka = {
      source  = "Mongey/kafka"
      version = "~> 0.7"
    }
  }
}

provider "kafka" {
  bootstrap_servers = ["localhost:9092"]
  ca_cert           = file("../secrets/ca.crt")
  client_cert       = file("../secrets/client-cert.pem")
  client_key        = file("../secrets/client-key.pem")
  tls_enabled       = true
}
```

### SASL/PLAIN Authentication

```terraform
provider "kafka" {
  bootstrap_servers = ["localhost:9092"]
  sasl_mechanism    = "plain"
  sasl_username     = "terraform"
  sasl_password     = var.kafka_password
  tls_enabled       = true
}
```

### SASL/SCRAM Authentication

```terraform
provider "kafka" {
  bootstrap_servers = ["localhost:9092"]
  sasl_mechanism    = "scram-sha256"
  sasl_username     = "terraform"
  sasl_password     = var.kafka_password
  tls_enabled       = true
}
```

### AWS MSK with IAM Authentication (Using AssumeRole)

```terraform
provider "kafka" {
  bootstrap_servers = ["b-1.msk-cluster.xxx.kafka.us-east-1.amazonaws.com:9098"]
  tls_enabled       = true
  sasl_mechanism    = "aws-iam"
  sasl_aws_region   = "us-east-1"
  sasl_aws_role_arn = "arn:aws:iam::123456789012:role/kafka-terraform-role"
}
```

### AWS MSK with IAM Authentication (Using AWS Profile)

```terraform
provider "kafka" {
  bootstrap_servers = ["b-1.msk-cluster.xxx.kafka.us-east-1.amazonaws.com:9098"]
  tls_enabled       = true
  sasl_mechanism    = "aws-iam"
  sasl_aws_region   = "us-east-1"
  sasl_aws_profile  = "production"
}
```

### AWS MSK with IAM Authentication (Using Static Credentials)

```terraform
# Example using HashiCorp Vault to retrieve temporary AWS credentials
provider "vault" {
  auth_login_jwt {
    role = "jwt-role-name"
  }
}

data "vault_aws_access_credentials" "creds" {
  backend = "aws"
  type    = "sts"
  role    = "kafka-access-role"
}

provider "kafka" {
  bootstrap_servers   = ["b-1.msk-cluster.xxx.kafka.us-east-1.amazonaws.com:9098"]
  tls_enabled         = true
  sasl_mechanism      = "aws-iam"
  sasl_aws_region     = "us-east-1"
  sasl_aws_access_key = data.vault_aws_access_credentials.creds.access_key
  sasl_aws_secret_key = data.vault_aws_access_credentials.creds.secret_key
  sasl_aws_token      = data.vault_aws_access_credentials.creds.security_token
}
```

### OAuth2 Authentication

```terraform
provider "kafka" {
  bootstrap_servers = ["localhost:9092"]
  sasl_mechanism    = "oauthbearer"
  sasl_token_url    = "https://oauth.example.com/oauth2/token"
  sasl_oauth_scopes = ["kafka:read", "kafka:write"]
  tls_enabled       = true
}
```

### Redpanda Compatibility

When using with [Redpanda](https://redpanda.com/), you may need to force a specific Kafka protocol version:

```terraform
provider "kafka" {
  bootstrap_servers = ["localhost:9092"]
  kafka_version     = "2.1.0"
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `bootstrap_servers` (List of String) A list of kafka brokers

### Optional

- `ca_cert` (String) CA certificate file to validate the server's certificate.
- `ca_cert_file` (String, Deprecated) Path to a CA certificate file to validate the server's certificate.
- `client_cert` (String) The client certificate.
- `client_cert_file` (String, Deprecated) Path to a file containing the client certificate.
- `client_key` (String) The private key that the certificate was issued for.
- `client_key_file` (String, Deprecated) Path to a file containing the private key that the certificate was issued for.
- `client_key_passphrase` (String) The passphrase for the private key that the certificate was issued for.
- `kafka_version` (String) The version of Kafka protocol to use in `$MAJOR.$MINOR.$PATCH` format. Some features may not be available on older versions. Default is 2.7.0.
- `sasl_aws_access_key` (String) The AWS access key.
- `sasl_aws_container_authorization_token_file` (String) Path to a file containing the AWS pod identity authorization token
- `sasl_aws_container_credentials_full_uri` (String) URI to retrieve AWS credentials from
- `sasl_aws_creds_debug` (Boolean) Set this to true to turn AWS credentials debug.
- `sasl_aws_external_id` (String) External ID of the AWS IAM role to assume
- `sasl_aws_profile` (String) AWS profile name to use
- `sasl_aws_region` (String) AWS region where MSK is deployed.
- `sasl_aws_role_arn` (String) Arn of an AWS IAM role to assume
- `sasl_aws_secret_key` (String) The AWS secret key.
- `sasl_aws_shared_config_files` (List of String) List of paths to AWS shared config files.
- `sasl_aws_token` (String) The AWS session token. Only required if you are using temporary security credentials.
- `sasl_mechanism` (String) SASL mechanism, can be plain, scram-sha512, scram-sha256, aws-iam
- `sasl_oauth_scopes` (List of String) OAuth scopes to request when using the oauthbearer mechanism
- `sasl_password` (String) Password for SASL authentication.
- `sasl_token_url` (String) The url to retrieve oauth2 tokens from, when using sasl mechanism oauthbearer
- `sasl_username` (String) Username for SASL authentication.
- `skip_tls_verify` (Boolean) Set this to true only if the target Kafka server is an insecure development instance.
- `timeout` (Number) Timeout in seconds
- `tls_enabled` (Boolean) Enable communication with the Kafka Cluster over TLS.